# https://github.com/TUM-DSE/doctor-cluster-config/tree/4702b65ba00ccaf932fa87c71eee5a5b584896ab/sops.yaml.nix
# IMPORTANT when changing this file, also run
# $ inv update-sops-files
# to update .sops.yaml:
let
  mapAttrsToList = f: attrs: map (name: f name attrs.${name}) (builtins.attrNames attrs);

  renderPermissions =
    attrs:
    mapAttrsToList (path: keys: {
      path_regex = path;
      key_groups = [ { age = keys ++ groups.admin; } ];
    }) attrs;

  # command to add a new age key for a new host
  # inv print-age-key --hosts "host1,host2"
  keys = builtins.fromJSON (builtins.readFile ./pubkeys.json);
  groups = with keys.users; {
    admin = [
      # admins may access all secrets
      seungwon
    ];
    all = builtins.attrValues (keys.users // keys.machines);
  };

  # This is the list of permissions per file. The admin group has permissions
  # for all files. amy.yml additionally can be decrypted by amy.
  sopsPermissions =
    # === secrets for each machines ===
    builtins.listToAttrs (
      mapAttrsToList (hostname: key: {
        name = "hosts/${hostname}.yaml$";
        value = [ key ];
      }) keys.machines
    )
    // builtins.mapAttrs (_name: value: (map (x: keys.machines.${x}) value)) {
      "modules/acme/secrets.yaml" = [
        "eta"
        "psi"
        "tau"
      ];
      "modules/authentik/secrets.yaml" = [ "eta" ];
      "modules/stalwart/secrets.yaml" = [
        "eta"
        "rho"
      ];
      "modules/headscale/secrets.yaml" = [ "eta" ];
      "modules/gatus/secrets.yaml" = [
        "eta"
        "psi"
        "rho"
        "tau"
      ];
      "modules/gitea-mq/secrets.yaml" = [ "eta" ];
      "modules/vaultwarden/secrets.yaml" = [ "eta" ];
      "modules/tailscale/secrets.yaml" = [
        "psi"
        "rho"
        "tau"
      ];
      "modules/borgbackup/psi/secrets.yaml" = [ "psi" ];
      "hosts/shared/psi-backup.yaml" = [
        "eta"
        "psi"
        "rho"
        "tau"
      ];
      "modules/buildbot/secrets.yaml" = [ "psi" ];
      "modules/container-registry/secrets.yaml" = [ "eta" ];
      "modules/monitoring/secrets.yaml" = [ "rho" ];
      "modules/hermes-agent/secrets.yaml" = [ "tau" ];
      "modules/n8n/secrets.yaml" = [ "tau" ];
      "modules/nextcloud/secrets.yaml" = [ "tau" ];
      "modules/nfs/secrets.yaml" = [ "psi" ];
      "modules/niks3/secrets.yaml" = [ "psi" ];
      "modules/users/xrdp-passwords.yaml" = [ "psi" ];
      "terraform/authentik/secrets.yaml" = [ ];
      "terraform/authentik/oidc-secrets.yaml" = [
        "eta"
        "tau"
      ];
      "terraform/authentik/users.yaml" = [ ];
      "terraform/alert-bridge/secrets.yaml" = [ ];
      "terraform/dns/secrets.yaml" = [ ];
      "terraform/r2/secrets.yaml" = [ ];
      "terraform/github/secrets.yaml" = [ ];
      "terraform/headscale/secrets.yaml" = [ ];
      "terraform/healthchecksio/secrets.yaml" = [ ];
      "terraform/vultr/secrets.yaml" = [ ];
    }
    // {
      "modules/sshd/[^/]+\\.yaml$" = [ ];
      "terraform/secrets.yaml" = [ ];
      "^\\.secrets\\.yaml$" = [ ];
    };
in
{
  creation_rules = renderPermissions sopsPermissions;
}
